//MOSHIXCV  JOB CLASS=A,MSGCLASS=H,MSGLEVEL=(1,1),NOTIFY=&SYSUID,
//          REGION=80M
//CLG       EXEC PROC=ASMACLG
//C.SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=SYS1.MODGEN
//SYSPRINT  DD SYSOUT=*,RECFM=FBA,LRECL=80
//G.REPORT  DD SYSOUT=*,RECFM=FBA,LRECL=80
//SNAPDUMP  DD SYSOUT=*
//C.SYSIN   DD *
***************************************************************
*          PROGRAM TO READ DATA FROM ZOS NUCLUS BASED ON
*          VARIOUS EXAMPLES ON WEB AND IBM REFERENCE WEBSITE
*          MARCH 25, 2018
***************************************************************
         PRINT GEN
MAIN     CSECT ,
MAIN     AMODE 31
MAIN     RMODE 24
***********ENTRY TO PROGRAM************************************
         SAVE    (14,12)                   SAVE REGS ON ENTRY
         BASR    R12,0                     SET UP BASE REGS LIKE BAL
         USING   *,R12                     S/360 STYLE ENTRY, I KNOW
         ST      R13,SAVEA+4               ENSURE SAVEA CHAIN BUILT
         LA      R13,SAVEA                 CORRECTLY
***********OPEN DATA SETS   ***********************************
         OPEN   (REPORT,OUTPUT)            OPEN REPORT PRINT DS
         PUT    REPORT,TITLINE1
         PUT    REPORT,TITLINE2
         PUT    REPORT,TITLINE3
***********ACCESS IPA       ***********************************
ACCIPA   EQU    *
         L      R1,CVTPTR                  LOAD CVT
         L      R1,CVTECVT-CVT(,R1)        ADR OF ECVT OFF R1
         L      R2,ECVTIPA-ECVT(,R1)       ADR OF IPA FROM ECVT FLD
         USING  IPA,R2                     CVT OR IHAIPA??
*********PREPARE REPORT ***************************************
         MVC    DEV,IPALPDDV               MOVE IPL DEV TO BUFFER
         MVC    CAT,IPASCVOL               MOVE MASTCAT VOL TO BUFFR
         PUT    REPORT,OUTLINE1            PRINT OUT THIS INFO
         PUT    REPORT,OUTLINE2            PRINT OUT CAT DEVICE
*********START TO TIDY UP AND CLOSE ***************************
RETURN   EQU    *
         CLOSE  (REPORT)
GOBACK   L      R13,SAVEA+4                 FIND REGS
         RETURN (14,12),RC=0               RELOAD REGS AND RETURN CD 0
*
         LTORG
***********DATA DEF  ******************************************
SAVEA    DS      18F                       SAVE AREA
*********DCBS FOR FILES ***************************************
REPORT   DCB    DDNAME=REPORT,DSORG=PS,MACRF=PM,RECFM=FB,BLKSIZE=0,    +
               LRECL=80
TITLINE1 DC     CL80'IPL PARAMETER REPORT YOUTUBE'
TITLINE2 DC     CL80'============================'
TITLINE3 DC     CL80'                            '
OUTLINE1 DC     CL80'IPL DEVICE=XXXX'      PRINT IPL DEVICE
DEV      EQU    OUTLINE1+11,4              DEV NUMBER IS 4 BYTES
OUTLINE2 DC     CL80'MCAT   DEV=XXXXXX'
CAT      EQU    OUTLINE2+11,6              VOLUMES ARE 6 BYTES
***********CVT ACCESS MACROS***********************************
         CVT    DSECT=YES,LIST=YES         CVT MAPPING
         IHAECVT DSECT=YES,LIST=YES        EXTENDED CVT
         IHAIPA ,                          IPL PARAMETERS
*
*********REG EQUATES AND PROGRAM END***************************
R0       EQU     0
R1       EQU     1
R2       EQU     2
R3       EQU     3
R4       EQU     4
R5       EQU     5
R6       EQU     6
R7       EQU     7
R8       EQU     8
R9       EQU     9
R10      EQU     10
R11      EQU     11
R12      EQU     12
R13      EQU     13
R14      EQU     14
R15      EQU     15
         END ,                             PROGRAM END. DEBUGGIN NOW
/*
//
